buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
        // needed for deployment to artifactory
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.1.1"
        // needed for sending code coverage to coveralls
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3"
        // Android APT plugin needed for Dagger 2
        classpath "com.neenbedankt.gradle.plugins:android-apt:1.8"
    }
}

ext {
    // configuration for loading the URL, username and password for the private Door2Door maven repo
    artifactoryUrl = project.hasProperty('artifactory_contextUrl') ? artifactory_contextUrl : System.getenv('artifactory_contextUrl')
    artifactoryUser = project.hasProperty('artifactory_user') ? artifactory_user : System.getenv('artifactory_user')
    artifactoryPassword = project.hasProperty('artifactory_password') ? artifactory_password : System.getenv('artifactory_password')

    computedVersion = computeVersion()
}

allprojects {
    repositories {
        jcenter()

        // Door2Door private release repository
        maven {
            url "${artifactoryUrl}/libs-release"
            credentials {
                username = artifactoryUser
                password = artifactoryPassword
            }
        }

        // Door2Door private snapshot repository
        maven {
            url "${artifactoryUrl}/libs-snapshot"
            credentials {
                username = artifactoryUser
                password = artifactoryPassword
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Method that removes the -SNAPSHOT part of the version if the build is running on travis from
// the master branch
def computeVersion() {
    def workingBranch = System.getenv("TRAVIS_BRANCH")
    println String.format('Working branch is [%s]', workingBranch)
    String rawVersion = project.rawVersion
    final String version
    if ('master'.equals(workingBranch)) {
        version = rawVersion.replace('-SNAPSHOT', '')
    } else {
        version = rawVersion
    }
    println String.format('Version is [%s]', version)
    return version
}